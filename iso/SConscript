# -*- mode: python -*-
Import(['env', 'kernel', 'user'])

# Create a bootable ISO image
iso = env.ISO(
    target = 'tacos.iso',
    source = [
        'root/boot/grub/menu.lst',
        'root/boot/grub/stage2_eltorito',
        'root/boot/tacos',
        'root/dash',
        'root/usr/bin/env',
    ],
    MKISOFSFLAGS = [
        '-R',
        '-b', 'boot/grub/stage2_eltorito',
        '-no-emul-boot',
        '-boot-load-size', '4',
        '-boot-info-table',
        '-quiet',
    ],
    ROOT = env.Dir('root'),
)[0]

def CopyFile(target, source):
    return env.Command(target, source, Copy('$TARGET', '$SOURCE'))

CopyFile(env.File('root/boot/grub/menu.lst'), env.File('#iso/root/boot/grub/menu.lst'))
CopyFile(env.File('root/boot/grub/stage2_eltorito'), env.File('#iso/root/boot/grub/stage2_eltorito'))
CopyFile(env.File('root/boot/tacos'), kernel)
CopyFile(env.File('root/dash'), env.File('#toolchain/install/bin/dash'))
for binary in user:
    binary.env.Strip(target = env.File('root/usr/bin/%s' % binary.name), source = binary)

env.Alias('tacos-iso', iso)
Return(['iso'])
