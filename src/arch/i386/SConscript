# -*- mode: python -*-
Import(['env', 'objects'])

# Build the i386-specific files.
objects += env.SConscript('boot/SConscript', exports=['env'])
objects += env.SConscript('intr/SConscript', exports=['env'])

# Process the linker script input file to generate the actual linker script.
linker_script = env.Sed('ld/link.ld', 'ld/link.ld.in',
                        PATTERN="s:@@STARTUP@@:$STARTUP:g",
                        STARTUP=env.File('boot/boot.o'))

# Modify the SCons environment for linking an i386 kernel.
env.Append(
    LINKFLAGS=['-T$LDSCRIPT'],
    LDSCRIPT=linker_script,
)

# Create the kernel.
kernel = env.Program('kernel', objects)
env.Depends(kernel, '$LDSCRIPT')
env.Depends(kernel, env.File('boot/boot.o'))

Return('kernel')
